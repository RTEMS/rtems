OUTPUT_FORMAT("elf32-lm32", "elf32-lm32",
              "elf32-lm32")
OUTPUT_ARCH(lm32)
ENTRY(_start)
/* Do we need any of these for elf?
   __DYNAMIC = 0;    */

/*
 * Declare some sizes. Heap is sized at whatever ram space is left.
 */
RamBase = DEFINED(RamBase) ? RamBase : 0x08000000;
RamSize = DEFINED(RamSize) ? RamSize : 32M;
HeapSize = DEFINED(HeapSize) ? HeapSize : 2M;
_StackSize = DEFINED(_StackSize) ? _StackSize : 0x2000;

PROVIDE (__stack = 0);
MEMORY {
	ebr	: ORIGIN = 0x04000000 , LENGTH = 32k
	sdram   : ORIGIN = 0x08000000 , LENGTH = 32M
}
SECTIONS
{
  .boot :
  {
    KEEP (*(.boot))
  } > ebr

  /* Read-only sections, merged into text segment: */
  .interp   : { *(.interp) }	> sdram
  .hash           : { *(.hash)          }	> sdram
  .dynsym         : { *(.dynsym)                }	> sdram
  .dynstr         : { *(.dynstr)                }	> sdram
  .gnu.version   : { *(.gnu.version)      }	> sdram
  .gnu.version_d   : { *(.gnu.version_d)  }	> sdram
  .gnu.version_r   : { *(.gnu.version_r)  }	> sdram
  .rela.text     :
    { *(.rela.text) *(.rela.gnu.linkonce.t*) }	> sdram
  .rela.data     :
    { *(.rela.data) *(.rela.gnu.linkonce.d*) }	> sdram
  .rela.rodata   :
    { *(.rela.rodata*) *(.rela.gnu.linkonce.r*) }	> sdram
  .rela.got       : { *(.rela.got)      }	> sdram
  .rela.got1      : { *(.rela.got1)     }	> sdram
  .rela.got2      : { *(.rela.got2)     }	> sdram
  .rela.ctors     : { *(.rela.ctors)    }	> sdram
  .rela.dtors     : { *(.rela.dtors)    }	> sdram
  .rela.init      : { *(.rela.init)     }	> sdram
  .rela.fini      : { *(.rela.fini)     }	> sdram
  .rela.bss       : { *(.rela.bss)      }	> sdram
  .rela.plt       : { *(.rela.plt)      }	> sdram
  .rela.sdata     : { *(.rela.sdata)    }	> sdram
  .rela.sbss      : { *(.rela.sbss)     }	> sdram
  .rela.sdata2    : { *(.rela.sdata2)   }	> sdram
  .rela.sbss2     : { *(.rela.sbss2)    }	> sdram
  .rela.dyn       : { *(.rela.dyn)      }	> sdram

  .init		  : { KEEP(*(.init))		} > sdram

  .text      :	
  {
    *(.text*)	

    /*
     * Special FreeBSD sysctl sections.
     */
    . = ALIGN (16);
    __start_set_sysctl_set = .;
    *(set_sysctl_*);
    __stop_set_sysctl_set = ABSOLUTE(.);
    *(set_domain_*);
    *(set_pseudo_*);

    /* .gnu.warning sections are handled specially by elf32.em.  */
    *(.gnu.warning)
    *(.gnu.linkonce.t*)
  }  > sdram

  .fini		  : { _fini = .; KEEP(*(.fini))	} > sdram

  .rodata         : { *(.rodata*) *(.gnu.linkonce.r*) }	> sdram
  .rodata1        : { *(.rodata1) }	> sdram

  /* Adjust the address for the data segment.  We want to adjust up to
     the same address within the page on the next page up.  */
  . = ALIGN(0x10000) + (. & (0x10000 - 1));
  /* Ensure the __preinit_array_start label is properly aligned.  We
     could instead move the label definition inside the section, but
     the linker would then create the section even if it turns out to
     be empty, which isn't pretty.  */
  . = ALIGN(32 / 8);
  PROVIDE (__preinit_array_start = .);
  .preinit_array     : { *(.preinit_array) } >sdram
  PROVIDE (__preinit_array_end = .);
  PROVIDE (__init_array_start = .);
  .init_array     : { *(.init_array) } >sdram
  PROVIDE (__init_array_end = .);
  PROVIDE (__fini_array_start = .);
  .fini_array     : { *(.fini_array) } >sdram
  PROVIDE (__fini_array_end = .);

/* _SDA2_BASE_ = __SDATA2_START__ + 0x8000; */
  .sdata2         : { *(.sdata2 .sdata2.* .gnu.linkonce.s2.*) } >sdram
  .sbss2          : { *(.sbss2 .sbss2.* .gnu.linkonce.sb2.*) 
                        /* avoid empty sdata2/sbss2 area -- __eabi would not set up r2
                         * which may be important if run-time loading is used
                         */
                     . += 1;
                    } >sdram
  .eh_frame	  : { *.(eh_frame)		} >sdram

  /* NOTE: if the BSP uses page tables, the correctness of
   *       '_etext' (and __DATA_START__) is CRUCIAL - otherwise,
   *       an invalid mapping may result!!!
   */
  _etext = .;
  PROVIDE (etext = .);

  /* Adjust the address for the data segment.  We want to adjust up to
     the same address within the page on the next page up.  It would
     be more correct to do this:
       . = ALIGN(0x40000) + (ALIGN(8) & (0x40000 - 1));
     The current expression does not correctly handle the case of a
     text segment ending precisely at the end of a page; it causes the
     data segment to skip a page.  The above expression does not have
     this problem, but it will currently (2/95) cause BFD to allocate
     a single segment, combining both text and data, for this case.
     This will prevent the text segment from being shared among
     multiple executions of the program; I think that is more
     important than losing a page of the virtual address space (note
     that no actual memory is lost; the page which is skipped can not
     be referenced).  */
  . = ALIGN(0x1000);
 .data ALIGN(0x1000) :
  {
   /* NOTE: if the BSP uses page tables, the correctness of
    *       '__DATA_START__' (and _etext) is CRUCIAL - otherwise,
    *       an invalid mapping may result!!!
    */
	PROVIDE(__DATA_START__ = ABSOLUTE(.) );
    *(.data .data.* .gnu.linkonce.d*)	
    SORT(CONSTRUCTORS)
  } > sdram
  .data1   : { *(.data1) }	> sdram
  PROVIDE (__EXCEPT_START__ = .);
  .gcc_except_table   : {
    *(.gcc_except_table)
    *(.gcc_except_table.*)
  } > sdram
  PROVIDE (__EXCEPT_END__ = .);
  .got1           : { *(.got1) }	> sdram
  /* Put .ctors and .dtors next to the .got2 section, so that the pointers
     get relocated with -mrelocatable. Also put in the .fixup pointers.
     The current compiler no longer needs this, but keep it around for 2.7.2  */
                PROVIDE (_GOT2_START_ = .);
  .got2           :  { *(.got2) }	> sdram
  .dynamic        : { *(.dynamic) }	> sdram

  .ctors          :
  {
    /* gcc uses crtbegin.o to find the start of
       the constructors, so we make sure it is
       first.  Because this is a wildcard, it
       doesn't matter if the user does not
       actually link against crtbegin.o; the
       linker won't look for a file to match a
       wildcard.  The wildcard also means that it
       doesn't matter which directory crtbegin.o
       is in.  */
    KEEP (*crtbegin.o(.ctors))
    /* We don't want to include the .ctor section from
       from the crtend.o file until after the sorted ctors.
       The .ctor section from the crtend file contains the
       end of ctors marker and it must be last */
    KEEP (*(EXCLUDE_FILE (*crtend.o ) .ctors))
    KEEP (*(SORT(.ctors.*)))
    KEEP (*(.ctors))
  } > sdram
  .dtors          :
  {
    KEEP (*crtbegin.o(.dtors))
    KEEP (*(EXCLUDE_FILE (*crtend.o ) .dtors))
    KEEP (*(SORT(.dtors.*)))
    KEEP (*(.dtors))
  } > sdram


                PROVIDE (_FIXUP_START_ = .);
  .fixup          : { *(.fixup) }	> sdram
                PROVIDE (_FIXUP_END_ = .);
                PROVIDE (_GOT2_END_ = .);
                PROVIDE (_GOT_START_ = .);

  .got            : { *(.got) }	> sdram
  .got.plt        : { *(.got.plt) }	> sdram

                PROVIDE (_GOT_END_ = .);

  .jcr            : { KEEP (*(.jcr)) } > sdram

  /* We want the small data sections together, so single-instruction offsets
     can access them all, and initialized data all before uninitialized, so
     we can shorten the on-disk segment size.  */
/* _SDA_BASE_ = __SDATA_START__ + 0x8000; */
  .sdata	  : { *(.sdata*) *(.gnu.linkonce.s.*)	} >sdram
  _edata  =  .;
  _gp = .;
  PROVIDE (edata = .);
  .sbss      :
  {
    _clear_start = .;
    PROVIDE (__sbss_start = .);
    *(.dynsbss)
    *(.sbss* .gnu.linkonce.sb.*)
    *(.scommon)
    /* avoid empty sdata/sbss area -- __eabi would not set up r13
     * which may be important if run-time loading is used
     */
    . += 1;
    . = ALIGN(16);
    PROVIDE (__sbss_end = .);
  } > sdram
  .plt   : { *(.plt) }	> sdram
  .bss       :
  {
   PROVIDE (__bss_start = .);
   *(.dynbss)
   *(.bss .bss* .gnu.linkonce.b*)
   *(COMMON)
  . = ALIGN(16);
  _end = . ;
  __rtems_end = . ;
  PROVIDE (end = .);

  . += _StackSize;
  _fstack = .;

  WorkAreaBase = .;

    . = ALIGN (16);
    _stack_init = .;
    _clear_end = .;

  } > sdram


  /DISCARD/ : 
  {
    *(.comment)
  }


  /* Stabs debugging sections.  */
  .stab 0 : { *(.stab) }
  .stabstr 0 : { *(.stabstr) }
  .stab.excl 0 : { *(.stab.excl) }
  .stab.exclstr 0 : { *(.stab.exclstr) }
  .stab.index 0 : { *(.stab.index) }
  .stab.indexstr 0 : { *(.stab.indexstr) }
  .comment 0 : { *(.comment) }

  /* DWARF debug sections.
     Symbols in the DWARF debugging sections are relative to the beginning
     of the section so we begin them at 0.  */
  /* DWARF 1 */
  .debug          0 : { *(.debug) }
  .line           0 : { *(.line) }
  
  /* GNU DWARF 1 extensions */
  .debug_srcinfo  0 : { *(.debug_srcinfo) }
  .debug_sfnames  0 : { *(.debug_sfnames) }
  
  /* DWARF 1.1 and DWARF 2 */
  .debug_aranges  0 : { *(.debug_aranges) }
  .debug_pubnames 0 : { *(.debug_pubnames) }
  
  /* DWARF 2 */
  .debug_info     0 : { *(.debug_info) }
  .debug_abbrev   0 : { *(.debug_abbrev) }
  .debug_line     0 : { *(.debug_line) }
  .debug_frame    0 : { *(.debug_frame) }
  .debug_str      0 : { *(.debug_str) }
  .debug_loc      0 : { *(.debug_loc) }
  .debug_macinfo  0 : { *(.debug_macinfo) }
  
  /* SGI/MIPS DWARF 2 extensions */
  .debug_weaknames 0 : { *(.debug_weaknames) }
  .debug_funcnames 0 : { *(.debug_funcnames) }
  .debug_typenames 0 : { *(.debug_typenames) }
  .debug_varnames  0 : { *(.debug_varnames) }
  /* These must appear regardless of  .  */
}
