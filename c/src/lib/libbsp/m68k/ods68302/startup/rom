/*
 * MC68302 Linker command file
 */

SECTIONS
{
  .text . :
  {
    text_start = .;
    *(.text)
    etext = .;
    . = ALIGN(4);
    __CTOR_LIST__ = .;
    LONG((__CTOR_END__ - __CTOR_LIST__) / 4 - 2)
    *(.ctors)
    LONG(0)
    __CTOR_END__ = .;
    . = ALIGN(4);
    __DTOR_LIST__ = .;
    LONG((__DTOR_END__ - __DTOR_LIST__) / 4 - 2)
    *(.dtors)
    LONG(0)
    __DTOR_END__ = .;
  }
  
  .vtable 0 :
  {
    vtable_start = .;
    *(.vtable)
    evtable = .;
  }

  .data (ADDR(.vtable) + SIZEOF(.vtable)) : 
     AT (ADDR(.text) + SIZEOF(.text))
  {
    data_start = .;
    *(.data)
    edata = .;
  }
  .bss (ADDR(.data) + SIZEOF(.data)) :
  {
    bss_start = .;
    *(.bss)
    *(COMMON)
    end = . ;
    _end = . ;
  }  
}

RAM_BASE = DEFINED(RAM_BASE) ? RAM_BASE : 0x00000000;
RAM_SIZE = DEFINED(RAM_SIZE) ? RAM_SIZE : 0x00100000;
ROM_BASE = DEFINED(ROM_BASE) ? ROM_BASE : 0x00C00000;
ROM_SIZE = DEFINED(ROM_SIZE) ? ROM_SIZE : 0x00100000;
MC68302_BASE = DEFINED(MC68302_BASE) ? MC68302_BASE : 0x00700000;
m302 = MC68302_BASE;
_VBR = 0;		/* location of the VBR table (in RAM) */


