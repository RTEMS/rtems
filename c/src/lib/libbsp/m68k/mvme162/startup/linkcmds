/*
 *  This file contains directives for the GNU linker which are specific
 *  to the Motorola MVME162 board.
 *
 *  COPYRIGHT (c) 1989-1999.
 *  On-Line Applications Research Corporation (OAR).
 *
 *  The license and distribution terms for this file may be
 *  found in the file LICENSE in this distribution or at
 *  http://www.OARcorp.com/rtems/license.html.
 *
 *  Modifications of respective RTEMS file: COPYRIGHT (c) 1994.
 *  EISCAT Scientific Association. M.Savitski
 *
 *  This material is a part of the MVME162 Board Support Package
 *  for the RTEMS executive. Its licensing policies are those of the
 *  RTEMS above.
 *
 *  $Id$
 */

StackSize = DEFINED(StackSize) ? StackSize : 0x1000;

MEMORY
        {
        ram : org = 0x100000, l = 1M
        }

SECTIONS
{
        .text 0x100000 :
        {
        text_start = . ;
        *(.text)
	. = ALIGN (16);

	*(.eh_fram)
	. = ALIGN (16);

	/*
	 * C++ constructors
	 */
	__CTOR_LIST__ = .;
	LONG((__CTOR_END__ - __CTOR_LIST__) / 4 - 2)
	*(.ctors)
	LONG(0)
	__CTOR_END__ = .;
	__DTOR_LIST__ = .;
	LONG((__DTOR_END__ - __DTOR_LIST__) / 4 - 2)
	*(.dtors)
	LONG(0)
	__DTOR_END__ = .;
        etext = ALIGN( 0x10 ) ;
        }
        .data : {
        data_start = . ;
        *(.data)
        edata = ALIGN( 0x10 ) ;
        }
        .bss : {
        bss_start = . ;
        *(.bss)
        *(COMMON)
        . += StackSize; /* XXX -- Old gld can't handle this */
        . = ALIGN (16);
        stack_init = .;
        end = . ;
        _end = . ;
        }
}
