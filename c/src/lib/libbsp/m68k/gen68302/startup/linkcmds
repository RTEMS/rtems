/*
 *  This file contains directives for the GNU linker which are specific
 *  to the Generic MC68302 board.
 * 
 *  COPYRIGHT (c) 1989-1998.
 *  On-Line Applications Research Corporation (OAR).
 *  Copyright assigned to U.S. Government, 1994.
 *
 *  The license and distribution terms for this file may be
 *  found in the file LICENSE in this distribution or at
 *  http://www.OARcorp.com/rtems/license.html.
 *
 *  $Id$
 */

MEMORY 
	{
	ram : org = 0x0000, l = 16M
	}

m302 = 0xf7f000;
_VBR = 0x000000;		/* location of the VBR table (in RAM) */

SECTIONS
{
        .text 0xc00000 :
	{
	text_start = . ;
        *(.text)
	. = ALIGN (16);
	*(.gnu.linkonce.t.*)
	*(.descriptors)
	/* .gnu.warning sections are handled specially by elf32.em.  */
	*(.gnu.warning)

	*(.eh_fram)
	. = ALIGN (16);

	/*
	 * C++ constructors
	 */
	__CTOR_LIST__ = .;
	LONG((__CTOR_END__ - __CTOR_LIST__) / 4 - 2)
	*(.ctors)
	LONG(0)
	__CTOR_END__ = .;
	__DTOR_LIST__ = .;
	LONG((__DTOR_END__ - __DTOR_LIST__) / 4 - 2)
	*(.dtors)
	LONG(0)
	__DTOR_END__ = .;

	_rodata_start = . ;
	*(.rodata)
	*(.gnu.linkonce.r*)
	_erodata = ALIGN( 0x10 ) ;

	etext = ALIGN( 0x10 ) ;
        } 
        .data 0x010000 : 
	{
	data_start = . ;
        *(.data)
    	*(.gnu.linkonce.d.*)
    	*(.gcc_except_table)
	edata = ALIGN( 0x10 ) ;
        }
        .bss ADDR( .data ) + SIZEOF( .data ): 
        {
	bss_start = . ;
	*(.bss)
	*(COMMON)
	end = . ;
	_end = . ;
	}  
}
