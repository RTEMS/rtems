/* This file is a derivation of that found with the newlib-1.6 distribution
 * for the idp.ld file.  That file, it appears, was originally written by
 * Rob Savoye.  Other ideas came from Joel Sherrill for the RTEMS linkcmds
 * file (this is basically a mixture of the two).
 *
 *  $Id$
 */

/*
 * Setup the memory map of the MC68ec0x0 Board (IDP)
 * stack grows up towards high memory. This works for
 * both the rom68k and the mon68k monitors.
 */

MEMORY 
{
     ram : org = 0x10000, l = 2M
}

/*
 * Declare some sizes.
 */
HeapSize = DEFINED(HeapSize) ? HeapSize : 0x10000;
StackSize = DEFINED(StackSize) ? StackSize : 0x1000;

SECTIONS
{
	ram : {
		. = .;
	} >ram

        /*
         * Text, data and bss segments
         */
        .text : {
                *(.text)

		/*
		 * C++ constructors/destructors
		 */
		*(.gnu.linkonce.t.*)

		/*
		 * Initialization and finalization code.
     		 *
     		 * Various files can provide initialization and finalization
		 * functions.  crtbegin.o and crtend.o are two instances. The
		 * body of these functions are in .init and .fini sections. We
		 * accumulate the bodies here, and prepend function prologues
		 * from crti.o and function epilogues from crtn.o. crti.o must
		 * be linked first; crtn.o must be linked last.  Because these
		 * are wildcards, it doesn't matter if the user does not
		 * actually link against crti.o and crtn.o; the linker won't
		 * look for a file to match a wildcard.  The wildcard also
		 * means that it doesn't matter which directory crti.o and
		 * crtn.o are in. 
		 */
		PROVIDE (_init = .);
		*crti.o(.init)
		*(.init)
		*crtn.o(.init)
		PROVIDE (_fini = .);
		*crti.o(.fini)
		*(.fini)
		*crtn.o(.fini)

		/*
		 * C++ constructors/destructors
     		 *
     		 * gcc uses crtbegin.o to find the start of the constructors
		 * and destructors so we make sure it is first.  Because this
		 * is a wildcard, it doesn't matter if the user does not
		 * actually link against crtbegin.o; the linker won't look for
		 * a file to match a wildcard.  The wildcard also means that
		 * it doesn't matter which directory crtbegin.o is in. The
		 * constructor and destructor list are terminated in
		 * crtend.o.  The same comments apply to it.
		 */
		. = ALIGN (16);
		*crtbegin.o(.ctors)
		*(.ctors)
		*crtend.o(.ctors)
		*crtbegin.o(.dtors)
		*(.dtors)
		*crtend.o(.dtors)

		/*
		 * Exception frame info
		 */
		. = ALIGN (16);
		*(.eh_frame)

		/*
		 * Read-only data
		 */
		. = ALIGN (16);
		_rodata_start = . ;
		*(.rodata)
		*(.gnu.linkonce.r*)

                 . = ALIGN (16);
                PROVIDE (_etext = .);
        } >ram
        .data : {
                PROVIDE (_copy_start = .);
                *(.data)
		*(.gnu.linkonce.d*)
		*(.gcc_except_table)
                . = ALIGN (16);
                PROVIDE (_edata = .);
                PROVIDE (_copy_end = .);
        } >ram
        .bss : {
                PROVIDE (_bss_start = .);
                PROVIDE (_clear_start = .);
                *(.bss)
                *(COMMON)
                . = ALIGN (16);
                PROVIDE (end = .);

                . += StackSize;
                PROVIDE (_stack_init = .);

                . = ALIGN (16);
                PROVIDE (_HeapStart = .);
                . += HeapSize;
                PROVIDE (_HeapEnd = .);

                clear_end = .;

                PROVIDE (_WorkspaceBase = .);
        } >ram
}
