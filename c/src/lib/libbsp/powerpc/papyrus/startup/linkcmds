/*
 *  This file contains directives for the GNU linker which are specific
 *  to the Papyrus.
 *
 *  $Id$
 */

OUTPUT_FORMAT("elf32-powerpc", "elf32-powerpc",
              "elf32-powerpc")
OUTPUT_ARCH(powerpc)
 SEARCH_DIR(/usr/local/powerpc-rtems/lib);
 
ENTRY(download_entry)
 
MEMORY
  {
        RAM : ORIGIN = 0, LENGTH = 4M
        FLASH : ORIGIN = 0xFF000000, LENGTH = 0x80000
        EPROM : ORIGIN = 0xFFFE0000, LENGTH = 0x20000
  }
 
/* Do we need any of these for elf?
   __DYNAMIC = 0;    */

/* What are these for? */

__EXCEPT_START__ = 0;
__EXCEPT_END__ = 0;
__SDATA2_START__ = 0;
__SDATA2_END__ = 0;
__SBSS2_START__ = 0;
__SBSS2_END__ = 0;
__FIXUP_START__ = 0;
__FIXUP_END__ = 0;
__GOT2_START__ = 0;
__GOT2_END__ = 0;
__SDATA_START__ = 0;
__SDATA_END__ = 0;


SECTIONS
{
  .vectors 0x0100 :
  {
    *(.vectors)
  }
 
  .text 0x20000 :
  {
     *(.entry)
     *(.entry2)
     *(.text)
     *(.rodata)
     *(.rodata1)
     *(.descriptors)
     *(rom_ver)
     etext = ALIGN(0x10);
     _etext = .;
     __CTOR_LIST__ = .;
     LONG((__CTOR_END__ - __CTOR_LIST__) / 4 - 2)
     *(.ctors)
     LONG(0)
     __CTOR_END__ = .;
     __DTOR_LIST__ = .;
     LONG((__DTOR_END__ - __DTOR_LIST__) / 4 - 2)
     *(.dtors)
     LONG(0)
     __DTOR_END__ = .;
     *(.lit)
     *(.shdata)
     *(.init)
     *(.fini)
     _endtext = .;
  } > RAM
 
  /* R/W Data */
  .data :
  {
    *(.data)
    *(.data1)
  } > RAM
 
  __GOT_START__ = .;
  .got :
  {
     s.got = .;
     *(.got.plt) *(.got)
  } > RAM
  __GOT_END__ = .;
 
  __SBSS_START__ = .;
  .bss :
  {
    bss.start = .;
    *(.bss) *(.sbss) *(COMMON)
    bss.end = ALIGN(4);
  } > RAM
  __SBSS_END__ = .;
 
  bss.size = bss.end - bss.start;
  PROVIDE(_end = bss.end);

  .line 0 : { *(.line) }
  .debug 0 : { *(.debug) }
  .debug_sfnames 0 : { *(.debug_sfnames) }
  .debug_srcinfo 0 : { *(.debug_srcinfo) }
  .debug_pubnames 0 : { *(.debug_pubnames) }
  .debug_aranges 0 : { *(.debug_aranges) }
  .debug_aregion 0 : { *(.debug_aregion) }
  .debug_macinfo 0 : { *(.debug_macinfo) }
  .stab 0 : { *(.stab) }
  .stabstr 0 : { *(.stabstr) }
}


