/*
 *  This file contains directives for the GNU linker which are specific
 *  to the Cyclone CVME960/CVME961 boards.
 *
 *  COPYRIGHT (c) 1989-1999.
 *  On-Line Applications Research Corporation (OAR).
 *
 *  The license and distribution terms for this file may be
 *  found in the file LICENSE in this distribution or at
 *  http://www.OARcorp.com/rtems/license.html.
 *
 *  $Id$
 */

MEMORY
        {
            ram : org = 0x0, l = 1M
        }

SECTIONS
{
        .text 0x10000 :
        {
            text_start = . ;
            _text_start = . ;
            *(.text)
	. = ALIGN (16);

	*(.eh_fram)
        *(.gnu.linkonce.t*)

	. = ALIGN (16);

	/*
	 * C++ constructors
	 */
	__CTOR_LIST__ = .;
	LONG((__CTOR_END__ - __CTOR_LIST__) / 4 - 2)
	*(.ctors)
	LONG(0)
	__CTOR_END__ = .;
	__DTOR_LIST__ = .;
	LONG((__DTOR_END__ - __DTOR_LIST__) / 4 - 2)
	*(.dtors)
	LONG(0)
	__DTOR_END__ = .;

        _rodata_start = . ;
        *(.rodata*)
        *(.gnu.linkonce.r*)
        _erodata = ALIGN( 0x10 ) ;
        etext = ALIGN( 0x10 ) ;
/*            _etext = .; -- conflicts */
        }
        .data ADDR( .text ) + SIZEOF( .text ):
        {
            data_start = . ;
            _data_start = .;
            *(.data)
            edata = ALIGN( 0x10 ) ;
/*            _edata = .; -- conflicts */
        }
        .bss ADDR( .data ) + SIZEOF( .data ):
        {
            bss_start = . ;
            _bss_start = . ;
            *(.bss)
            *(COMMON)
            end = . ;
            _end = . ;
        }
}
