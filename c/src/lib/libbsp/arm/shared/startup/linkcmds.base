/**
 * @file
 *
 * @ingroup bsp_linker
 *
 * @brief Linker command base file.
 */

/*
 * Copyright (c) 2008-2010 embedded brains GmbH.  All rights reserved.
 *
 *  embedded brains GmbH
 *  Obere Lagerstr. 30
 *  82178 Puchheim
 *  Germany
 *  <rtems@embedded-brains.de>
 *
 * The license and distribution terms for this file may be
 * found in the file LICENSE in this distribution or at
 * http://www.rtems.com/license/LICENSE.
 */

OUTPUT_FORMAT ("elf32-littlearm", "elf32-bigarm", "elf32-littlearm")

OUTPUT_ARCH (arm)

ENTRY (_start)

/*
 * Global symbols that may be defined externally
 */

bsp_vector_table_size = DEFINED (bsp_vector_table_size) ? bsp_vector_table_size : 64;

bsp_section_vbarrier_align = DEFINED (bsp_section_vbarrier_align) ? bsp_section_vbarrier_align : 1;
bsp_section_robarrier_align = DEFINED (bsp_section_robarrier_align) ? bsp_section_robarrier_align : 1;

bsp_stack_align = DEFINED (bsp_stack_align) ? bsp_stack_align : 8;

bsp_stack_abt_size = DEFINED (bsp_stack_abt_size) ? bsp_stack_abt_size : 0;
bsp_stack_abt_size = ALIGN (bsp_stack_abt_size, bsp_stack_align);

bsp_stack_fiq_size = DEFINED (bsp_stack_fiq_size) ? bsp_stack_fiq_size : 0;
bsp_stack_fiq_size = ALIGN (bsp_stack_fiq_size, bsp_stack_align);

bsp_stack_irq_size = DEFINED (bsp_stack_irq_size) ? bsp_stack_irq_size : 0;
bsp_stack_irq_size = ALIGN (bsp_stack_irq_size, bsp_stack_align);

bsp_stack_svc_size = DEFINED (bsp_stack_svc_size) ? bsp_stack_svc_size : 0;
bsp_stack_svc_size = ALIGN (bsp_stack_svc_size, bsp_stack_align);

bsp_stack_und_size = DEFINED (bsp_stack_und_size) ? bsp_stack_und_size : 0;
bsp_stack_und_size = ALIGN (bsp_stack_und_size, bsp_stack_align);

bsp_stack_main_size = DEFINED (bsp_stack_main_size) ? bsp_stack_main_size : 0;
bsp_stack_main_size = ALIGN (bsp_stack_main_size, bsp_stack_align);

/* FIXME: This should go away when LD is fixed */
bsp_section_align = DEFINED (bsp_section_align) ? bsp_section_align : 32;

SECTIONS {
	.start : {
		bsp_section_start_begin = .;
		KEEP (*(.bsp_start_text))
		KEEP (*(.bsp_start_data))
		. = ALIGN (bsp_section_align);
		bsp_section_start_end = .;
	} > REGION_START AT > REGION_START
	bsp_section_start_size = bsp_section_start_end - bsp_section_start_begin;

	.vector : {
		bsp_section_vector_begin = .;

		. = . + DEFINED (bsp_vector_table_in_start_section) ? 0 : bsp_vector_table_size;

		. = ALIGN (bsp_stack_align);

		bsp_stack_abt_begin = .;
		. = . + bsp_stack_abt_size;
		bsp_stack_abt_end = .;

		bsp_stack_fiq_begin = .;
		. = . + bsp_stack_fiq_size;
		bsp_stack_fiq_end = .;

		bsp_stack_irq_begin = .;
		. = . + bsp_stack_irq_size;
		bsp_stack_irq_end = .;

		bsp_stack_svc_begin = .;
		. = . + bsp_stack_svc_size;
		bsp_stack_svc_end = .;

		bsp_stack_und_begin = .;
		. = . + bsp_stack_und_size;
		bsp_stack_und_end = .;

		bsp_stack_main_begin = .;
		. = . + bsp_stack_main_size;
		bsp_stack_main_end = .;

		*(.bsp_vector)
		. = ALIGN (bsp_section_align);
		bsp_section_vector_end = .;
	} > REGION_VECTOR AT > REGION_VECTOR
	bsp_section_vector_size = bsp_section_vector_end - bsp_section_vector_begin;
	bsp_vector_table_begin = DEFINED (bsp_vector_table_in_start_section) ? bsp_section_start_begin : bsp_section_vector_begin;
	bsp_vector_table_end = bsp_vector_table_begin + bsp_vector_table_size;

	.vbarrier : {
		. = ALIGN (bsp_section_vbarrier_align);
	} > REGION_VECTOR

	.interp : {
		bsp_section_text_begin = .;
		*(.interp)
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.note.gnu.build-id : {
		*(.note.gnu.build-id)
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.hash : {
		*(.hash)
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.gnu.hash : {
		*(.gnu.hash)
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.dynsym : {
		*(.dynsym)
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.dynstr : {
		*(.dynstr)
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.gnu.version : {
		*(.gnu.version)
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.gnu.version_d : {
		*(.gnu.version_d)
		. = ALIGN (bsp_section_align);
	}
	> REGION_TEXT AT > REGION_TEXT_LOAD
	.gnu.version_r : {
		*(.gnu.version_r)
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.rel.dyn : {
		*(.rel.init)
		*(.rel.text .rel.text.* .rel.gnu.linkonce.t.*)
		*(.rel.fini)
		*(.rel.rodata .rel.rodata.* .rel.gnu.linkonce.r.*)
		*(.rel.data.rel.ro* .rel.gnu.linkonce.d.rel.ro.*)
		*(.rel.data .rel.data.* .rel.gnu.linkonce.d.*)
		*(.rel.tdata .rel.tdata.* .rel.gnu.linkonce.td.*)
		*(.rel.tbss .rel.tbss.* .rel.gnu.linkonce.tb.*)
		*(.rel.ctors)
		*(.rel.dtors)
		*(.rel.got)
		*(.rel.bss .rel.bss.* .rel.gnu.linkonce.b.*)
		PROVIDE_HIDDEN (__rel_iplt_start = .);
		*(.rel.iplt)
		PROVIDE_HIDDEN (__rel_iplt_end = .);
		PROVIDE_HIDDEN (__rela_iplt_start = .);
		PROVIDE_HIDDEN (__rela_iplt_end = .);
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.rela.dyn : {
		*(.rela.init)
		*(.rela.text .rela.text.* .rela.gnu.linkonce.t.*)
		*(.rela.fini)
		*(.rela.rodata .rela.rodata.* .rela.gnu.linkonce.r.*)
		*(.rela.data .rela.data.* .rela.gnu.linkonce.d.*)
		*(.rela.tdata .rela.tdata.* .rela.gnu.linkonce.td.*)
		*(.rela.tbss .rela.tbss.* .rela.gnu.linkonce.tb.*)
		*(.rela.ctors)
		*(.rela.dtors)
		*(.rela.got)
		*(.rela.bss .rela.bss.* .rela.gnu.linkonce.b.*)
		PROVIDE_HIDDEN (__rel_iplt_start = .);
		PROVIDE_HIDDEN (__rel_iplt_end = .);
		PROVIDE_HIDDEN (__rela_iplt_start = .);
		*(.rela.iplt)
		PROVIDE_HIDDEN (__rela_iplt_end = .);
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.rel.plt : {
		*(.rel.plt)
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.rela.plt : {
		*(.rela.plt)
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.init : {
		KEEP (*(.init))
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.fini : {
		KEEP (*(.fini))
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.plt : {
		*(.plt)
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.iplt : {
		*(.iplt)
		. = ALIGN (bsp_section_align);
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.text : {
		*(.text.unlikely .text.*_unlikely)
		*(.text .stub .text.* .gnu.linkonce.t.*)
		/* .gnu.warning sections are handled specially by elf32.em.  */
		*(.gnu.warning)
		*(.glue_7t) *(.glue_7) *(.vfp11_veneer) *(.v4_bx)
		. = ALIGN (bsp_section_align);
		bsp_section_text_end = .;
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	bsp_section_text_size = bsp_section_text_end - bsp_section_text_begin;
	bsp_section_text_load_begin = LOADADDR (.interp);
	bsp_section_text_load_end = bsp_section_text_load_begin + bsp_section_text_size;

	.rodata1 : {
		bsp_section_rodata_begin = .;
		*(.rodata1)
		. = ALIGN (bsp_section_align);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.ARM.extab : {
		*(.ARM.extab* .gnu.linkonce.armextab.*)
		. = ALIGN (bsp_section_align);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.ARM.exidx : {
		__exidx_start = .;
		*(.ARM.exidx* .gnu.linkonce.armexidx.*)
		__exidx_end = .;
		. = ALIGN (bsp_section_align);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.eh_frame : {
		KEEP (*(.eh_frame))
		. = ALIGN (bsp_section_align);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.gcc_except_table : {
		*(.gcc_except_table .gcc_except_table.*)
		. = ALIGN (bsp_section_align);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.preinit_array : {
		PROVIDE_HIDDEN (__preinit_array_start = .);
		KEEP (*(.preinit_array))
		PROVIDE_HIDDEN (__preinit_array_end = .);
		. = ALIGN (bsp_section_align);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.init_array : {
		PROVIDE_HIDDEN (__init_array_start = .);
		KEEP (*(SORT(.init_array.*)))
		KEEP (*(.init_array))
		PROVIDE_HIDDEN (__init_array_end = .);
		. = ALIGN (bsp_section_align);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.fini_array : {
		PROVIDE_HIDDEN (__fini_array_start = .);
		KEEP (*(.fini_array))
		KEEP (*(SORT(.fini_array.*)))
		PROVIDE_HIDDEN (__fini_array_end = .);
		. = ALIGN (bsp_section_align);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.ctors : {
		/* gcc uses crtbegin.o to find the start of
		   the constructors, so we make sure it is
		   first.  Because this is a wildcard, it
		   doesn't matter if the user does not
		   actually link against crtbegin.o; the
		   linker won't look for a file to match a
		   wildcard.  The wildcard also means that it
		   doesn't matter which directory crtbegin.o
		   is in.  */
		KEEP (*crtbegin.o(.ctors))
		KEEP (*crtbegin?.o(.ctors))
		/* We don't want to include the .ctor section from
		   the crtend.o file until after the sorted ctors.
		   The .ctor section from the crtend file contains the
		   end of ctors marker and it must be last */
		KEEP (*(EXCLUDE_FILE (*crtend.o *crtend?.o ) .ctors))
		KEEP (*(SORT(.ctors.*)))
		KEEP (*(.ctors))
		. = ALIGN (bsp_section_align);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.dtors : {
		KEEP (*crtbegin.o(.dtors))
		KEEP (*crtbegin?.o(.dtors))
		KEEP (*(EXCLUDE_FILE (*crtend.o *crtend?.o ) .dtors))
		KEEP (*(SORT(.dtors.*)))
		KEEP (*(.dtors))
		. = ALIGN (bsp_section_align);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.data.rel.ro : {
		*(.data.rel.ro.local* .gnu.linkonce.d.rel.ro.local.*)
		*(.data.rel.ro* .gnu.linkonce.d.rel.ro.*)
		. = ALIGN (bsp_section_align);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.jcr : {
		KEEP (*(.jcr))
		. = ALIGN (bsp_section_align);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.dynamic : {
		*(.dynamic)
		. = ALIGN (bsp_section_align);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.got : {
		*(.got.plt) *(.igot.plt) *(.got) *(.igot)
		. = ALIGN (bsp_section_align);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.rodata : {
		/* Special FreeBSD sysctl sections */
		. = ALIGN (16);
		__start_set_sysctl_set = .;
		*(set_sysctl_*);
		__stop_set_sysctl_set = ABSOLUTE(.);
		*(set_domain_*);
		*(set_pseudo_*);
		_bsd__start_set_sysinit_set = .;
		*(_bsd_set_sysinit_set);
		_bsd__stop_set_sysinit_set = .;
		_bsd__start_set_modmetadata_set = .;
		*(_bsd_set_modmetadata_set);
		_bsd__stop_set_modmetadata_set = .;
		_bsd__start_set_sysctl_set = .;
		*(_bsd_set_sysctl_set);
		_bsd__stop_set_sysctl_set = .;

		*(.rodata .rodata.* .gnu.linkonce.r.*)
		. = ALIGN (bsp_section_align);
		bsp_section_rodata_end = .;
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	bsp_section_rodata_size = bsp_section_rodata_end - bsp_section_rodata_begin;
	bsp_section_rodata_load_begin = LOADADDR (.rodata1);
	bsp_section_rodata_load_end = bsp_section_rodata_load_begin + bsp_section_rodata_size;

	.robarrier : {
		. = ALIGN (bsp_section_robarrier_align);
	} > REGION_RODATA

	.data1 : {
		bsp_section_data_begin = .;
		*(.data1)
		. = ALIGN (bsp_section_align);
	} > REGION_DATA AT > REGION_DATA_LOAD
	.data : {
		*(.data .data.* .gnu.linkonce.d.*)
		SORT(CONSTRUCTORS)
		. = ALIGN (bsp_section_align);
		bsp_section_data_end = .;
	} > REGION_DATA AT > REGION_DATA_LOAD
	bsp_section_data_size = bsp_section_data_end - bsp_section_data_begin;
	bsp_section_data_load_begin = LOADADDR (.data1);
	bsp_section_data_load_end = bsp_section_data_load_begin + bsp_section_data_size;

	.fast_text : {
		bsp_section_fast_text_begin = .;
		*(.bsp_fast_text)
		. = ALIGN (bsp_section_align);
		bsp_section_fast_text_end = .;
	} > REGION_FAST_TEXT AT > REGION_FAST_TEXT_LOAD
	bsp_section_fast_text_size = bsp_section_fast_text_end - bsp_section_fast_text_begin;
	bsp_section_fast_text_load_begin = LOADADDR (.fast_text);
	bsp_section_fast_text_load_end = bsp_section_fast_text_load_begin + bsp_section_fast_text_size;

	.fast_data : {
		bsp_section_fast_data_begin = .;
		*(.bsp_fast_data)
		. = ALIGN (bsp_section_align);
		bsp_section_fast_data_end = .;
	} > REGION_FAST_DATA AT > REGION_FAST_DATA_LOAD
	bsp_section_fast_data_size = bsp_section_fast_data_end - bsp_section_fast_data_begin;
	bsp_section_fast_data_load_begin = LOADADDR (.fast_data);
	bsp_section_fast_data_load_end = bsp_section_fast_data_load_begin + bsp_section_fast_data_size;

	.bss : {
		bsp_section_bss_begin = .;
		*(.dynbss)
		*(.bss .bss.* .gnu.linkonce.b.*)
		*(COMMON)
		. = ALIGN (bsp_section_align);
		bsp_section_bss_end = .;
	} > REGION_BSS AT > REGION_BSS
	bsp_section_bss_size = bsp_section_bss_end - bsp_section_bss_begin;

	.work : {
		/*
		 * The work section will occupy the remaining REGION_WORK region and
		 * contains the RTEMS work space and heap.
		 */
		bsp_section_work_begin = .;
		. += ORIGIN (REGION_WORK) + LENGTH (REGION_WORK) - ABSOLUTE (.);
		bsp_section_work_end = .;
	} > REGION_WORK AT > REGION_WORK
	bsp_section_work_size = bsp_section_work_end - bsp_section_work_begin;

	.stack : {
		/*
		 * The stack section will occupy the remaining REGION_STACK region and may
		 * contain the task stacks.  Depending on the region distribution this
		 * section may be of zero size.
		 */
		bsp_section_stack_begin = .;
		. += ORIGIN (REGION_STACK) + LENGTH (REGION_STACK) - ABSOLUTE (.);
		bsp_section_stack_end = .;
	} > REGION_STACK AT > REGION_STACK
	bsp_section_stack_size = bsp_section_stack_end - bsp_section_stack_begin;

	/* FIXME */
	RamBase = ORIGIN (REGION_WORK);
	RamSize = LENGTH (REGION_WORK);
	WorkAreaBase = bsp_section_work_begin;
	HeapSize = 0;

	/* Stabs debugging sections.  */
	.stab          0 : { *(.stab) }
	.stabstr       0 : { *(.stabstr) }
	.stab.excl     0 : { *(.stab.excl) }
	.stab.exclstr  0 : { *(.stab.exclstr) }
	.stab.index    0 : { *(.stab.index) }
	.stab.indexstr 0 : { *(.stab.indexstr) }
	.comment       0 : { *(.comment) }
	/* DWARF debug sections.
	   Symbols in the DWARF debugging sections are relative to the beginning
	   of the section so we begin them at 0.  */
	/* DWARF 1 */
	.debug          0 : { *(.debug) }
	.line           0 : { *(.line) }
	/* GNU DWARF 1 extensions */
	.debug_srcinfo  0 : { *(.debug_srcinfo) }
	.debug_sfnames  0 : { *(.debug_sfnames) }
	/* DWARF 1.1 and DWARF 2 */
	.debug_aranges  0 : { *(.debug_aranges) }
	.debug_pubnames 0 : { *(.debug_pubnames) }
	/* DWARF 2 */
	.debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
	.debug_abbrev   0 : { *(.debug_abbrev) }
	.debug_line     0 : { *(.debug_line) }
	.debug_frame    0 : { *(.debug_frame) }
	.debug_str      0 : { *(.debug_str) }
	.debug_loc      0 : { *(.debug_loc) }
	.debug_macinfo  0 : { *(.debug_macinfo) }
	/* SGI/MIPS DWARF 2 extensions */
	.debug_weaknames 0 : { *(.debug_weaknames) }
	.debug_funcnames 0 : { *(.debug_funcnames) }
	.debug_typenames 0 : { *(.debug_typenames) }
	.debug_varnames  0 : { *(.debug_varnames) }
	/* DWARF 3 */
	.debug_pubtypes 0 : { *(.debug_pubtypes) }
	.debug_ranges   0 : { *(.debug_ranges) }
	.ARM.attributes 0 : { KEEP (*(.ARM.attributes)) KEEP (*(.gnu.attributes)) }
	.note.gnu.arm.ident 0 : { KEEP (*(.note.gnu.arm.ident)) }
	/DISCARD/ : { *(.note.GNU-stack) *(.gnu_debuglink) *(.gnu.lto_*) }

	/* Catch all unknown sections */
	.nirvana : { *(*) } > NIRVANA
}
