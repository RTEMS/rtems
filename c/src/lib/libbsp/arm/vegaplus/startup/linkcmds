OUTPUT_FORMAT("elf32-littlearm", "elf32-bigarm",
	      "elf32-littlearm")
OUTPUT_ARCH(arm)
ENTRY(_start)
MEMORY {
	FLASH (rx)  : ORIGIN = 0x200000, LENGTH = 512K
	RAM   (!rx) : ORIGIN = 0x400000, LENGTH = 128K
}

/* Do we need any of these for elf?
   __DYNAMIC = 0;    */
SECTIONS
{
  /* Read-only sections, merged into text segment: */
/*  . = 0 + SIZEOF_HEADERS;*/
  .hash          : { *(.hash)			} > FLASH 
  .dynsym        : { *(.dynsym)			} > FLASH 
  .dynstr        : { *(.dynstr)			} > FLASH 
  .gnu.version   : { *(.gnu.version)		} > FLASH 
  .gnu.version_d   : { *(.gnu.version_d)	} > FLASH 
  .gnu.version_r   : { *(.gnu.version_r)	} > FLASH 
  .rel.init      : { *(.rel.init)		} > FLASH 
  .rela.init     : { *(.rela.init)		} > FLASH 
  .rel.text      :
    {
      *(.rel.text)
      *(.rel.text.*)
      *(.rel.gnu.linkonce.t*)
    } > FLASH 
  .rela.text     :
    {
      *(.rela.text)
      *(.rela.text.*)
      *(.rela.gnu.linkonce.t*)
    } > FLASH 
  .rel.fini      : { *(.rel.fini)	} > FLASH 
  .rela.fini     : { *(.rela.fini)	} > FLASH 
  .rel.rodata    :
    {
      *(.rel.rodata)
      *(.rel.rodata.*)
      *(.rel.gnu.linkonce.r*)
    } > FLASH 
  .rela.rodata   :
    {
      *(.rela.rodata)
      *(.rela.rodata.*)
      *(.rela.gnu.linkonce.r*)
    } > FLASH 
  .rel.data      :
    {
      *(.rel.data)
      *(.rel.data.*)
      *(.rel.gnu.linkonce.d*)
    } > FLASH 
  .rela.data     :
    {
      *(.rela.data)
      *(.rela.data.*)
      *(.rela.gnu.linkonce.d*)
    } > FLASH 
  .rel.ctors     : { *(.rel.ctors)	} > FLASH 
  .rela.ctors    : { *(.rela.ctors)	} > FLASH 
  .rel.dtors     : { *(.rel.dtors)	} > FLASH 
  .rela.dtors    : { *(.rela.dtors)	} > FLASH 
  .rel.got       : { *(.rel.got)	} > FLASH 
  .rela.got      : { *(.rela.got)	} > FLASH 
  .rel.sdata     :
    {
      *(.rel.sdata)
      *(.rel.sdata.*)
      *(.rel.gnu.linkonce.s*)
    } > FLASH 
  .rela.sdata     :
    {
      *(.rela.sdata)
      *(.rela.sdata.*)
      *(.rela.gnu.linkonce.s*)
    } > FLASH 
  .rel.sbss      : { *(.rel.sbss)		} > FLASH 
  .rela.sbss     : { *(.rela.sbss)		} > FLASH 
  .rel.bss       : { *(.rel.bss)		} > FLASH 
  .rela.bss      : { *(.rela.bss)		} > FLASH 
  .rel.plt       : { *(.rel.plt)		} > FLASH 
  .rela.plt      : { *(.rela.plt)		} > FLASH 
  .init          : 
  { 
    KEEP (*(.init))
  } > FLASH   /*=0*/
  .plt      : { *(.plt)	} > FLASH 
  .text      :
  {
    *(.text)
    *(.text.*)
    *(.stub)

    /*
     * Special FreeBSD sysctl sections.
     */
    . = ALIGN (16);
    __start_set_sysctl_set = .;
    *(set_sysctl_*);
    __stop_set_sysctl_set = ABSOLUTE(.);
    *(set_domain_*);
    *(set_pseudo_*);

    /* .gnu.warning sections are handled specially by elf32.em.  */
    *(.gnu.warning)
    *(.gnu.linkonce.t*)
    *(.glue_7t) *(.glue_7)
  } > FLASH  /*=0*/
  _etext = .;
  PROVIDE (etext = .);
  .fini      :
  {
    KEEP (*(.fini))
  } > FLASH  /*=0*/
  .rodata   : { *(.rodata) *(.rodata.*) *(.gnu.linkonce.r*) } > FLASH
  .rodata1   : { *(.rodata1) } > FLASH
  _erodata = ABSOLUTE(.);
  PROVIDE (erodata =  ABSOLUTE(.));
  /* Adjust the address for the data segment.  We want to adjust up to
     the same address within the page on the next page up.  */
  . = ALIGN(256);
  _begdata = ALIGN(256);
  .arm_end_text :
  {
    . = ALIGN(256);
    _initdata = ALIGN(256);
  } > FLASH	
  .data : AT (_begdata)
  {
    *(.data*)
    *(.jcr)
    *(.gnu.linkonce.d*)
    SORT(CONSTRUCTORS)
  } > RAM 
  .data1   : { *(.data1) } > RAM
  .eh_frame : { *(.eh_frame) } > RAM
  .gcc_except_table : { *(.gcc_except_table) } > RAM
  .ctors   : 
  {
    /* gcc uses crtbegin.o to find the start of
       the constructors, so we make sure it is
       first.  Because this is a wildcard, it
       doesn't matter if the user does not
       actually link against crtbegin.o; the
       linker won't look for a file to match a
       wildcard.  The wildcard also means that it
       doesn't matter which directory crtbegin.o
       is in.  */
    KEEP (*crtbegin.o(.ctors))
    /* We don't want to include the .ctor section from
       from the crtend.o file until after the sorted ctors.
       The .ctor section from the crtend file contains the
       end of ctors marker and it must be last */
    KEEP (*(EXCLUDE_FILE (*crtend.o ) .ctors))
    KEEP (*(SORT(.ctors.*)))
    KEEP (*(.ctors))
  } > RAM
   .dtors         :
  {
    KEEP (*crtbegin.o(.dtors))
    KEEP (*(EXCLUDE_FILE (*crtend.o ) .dtors))
    KEEP (*(SORT(.dtors.*)))
    KEEP (*(.dtors))
  } > RAM
  .got           : { *(.got.plt) *(.got) } > RAM
  .dynamic       : { *(.dynamic) } > RAM
  /* We want the small data sections together, so single-instruction offsets
     can access them all, and initialized data all before uninitialized, so
     we can shorten the on-disk segment size.  */
  .sdata     : 
  {
    *(.sdata) 
    *(.sdata.*)
    *(.gnu.linkonce.s.*)
  } > RAM
  .arm_data : 
  {  
  _edata = .; 
  __bss_start = .;
  __bss_start__ = .;
  } > RAM
  .sbss      :
  {
    *(.dynsbss)
    *(.sbss)
    *(.sbss.*)
    *(.scommon)
  } > RAM
  .bss       :
  {
   *(.dynbss)
   *(.bss)
   *(.bss.*)
   *(COMMON)
   /* Align here to ensure that the .bss section occupies space up to
      _end.  Align after .bss to ensure correct alignment even if the
      .bss section disappears because there are no input sections.  */
   . = ALIGN(32 / 8);
  } > RAM
  . = ALIGN(32 / 8);
  .arm_end :
  {
  _end = .; 
  _bss_end__ = . ; 
  __bss_end__ = . ; 
  __end__ = . ;
  } > RAM 
  /* Stabs debugging sections.  */
  .stab 0 : { *(.stab) } 
  .stabstr 0 : { *(.stabstr) } 
  .stab.excl 0 : { *(.stab.excl) } 
  .stab.exclstr 0 : { *(.stab.exclstr) }
  .stab.index 0 : { *(.stab.index) } 
  .stab.indexstr 0 : { *(.stab.indexstr) } 
  .comment 0 : { *(.comment) }
  /* DWARF debug sections.
     Symbols in the DWARF debugging sections are relative to the beginning
     of the section so we begin them at 0.  */
  /* DWARF 1 */
  .debug          0 : { *(.debug) }
  .line           0 : { *(.line) }
  /* GNU DWARF 1 extensions */
  .debug_srcinfo  0 : { *(.debug_srcinfo) }
  .debug_sfnames  0 : { *(.debug_sfnames) }
  /* DWARF 1.1 and DWARF 2 */
  .debug_aranges  0 : { *(.debug_aranges) }
  .debug_pubnames 0 : { *(.debug_pubnames) }
  /* DWARF 2 */
  .debug_info     0 : { *(.debug_info) }
  .debug_abbrev   0 : { *(.debug_abbrev) }
  .debug_line     0 : { *(.debug_line) }
  .debug_frame    0 : { *(.debug_frame) }
  .debug_str      0 : { *(.debug_str) } 
  .debug_loc      0 : { *(.debug_loc) }
  .debug_macinfo  0 : { *(.debug_macinfo) } 
  /* SGI/MIPS DWARF 2 extensions */
  .debug_weaknames 0 : { *(.debug_weaknames) }
  .debug_funcnames 0 : { *(.debug_funcnames) }
  .debug_typenames 0 : { *(.debug_typenames) }
  .debug_varnames  0 : { *(.debug_varnames) } 
  /*.stack 0x80000 : { _stack = .; *(.stack) }*/
  /* These must appear regardless of  .  */
}




