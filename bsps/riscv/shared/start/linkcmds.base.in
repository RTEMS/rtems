/* Copyright (C) 2014-2018 Free Software Foundation, Inc.
   Copying and distribution of this script, with or without modification,
   are permitted in any medium without royalty provided the copyright
   notice and this notice are preserved.  */

/*
 * Copyright (c) 2018 embedded brains GmbH.
 *
 * Copyright (c) 2015 University of York.
 * Hesham ALMatary <hmka501@york.ac.uk>
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR AND CONTRIBUTORS ``AS IS'' AND
 * ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED.  IN NO EVENT SHALL THE AUTHOR OR CONTRIBUTORS BE LIABLE
 * FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
 * OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
 * HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
 * LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
 * OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */

OUTPUT_ARCH(riscv)
ENTRY(_start)
@RISCV_LINKER_START_DIRECTIVE@(start.o)

bsp_section_robarrier_align = DEFINED (bsp_section_robarrier_align) ? bsp_section_robarrier_align : 1;
bsp_section_rwbarrier_align = DEFINED (bsp_section_rwbarrier_align) ? bsp_section_rwbarrier_align : 1;

MEMORY {
	UNEXPECTED_SECTIONS : ORIGIN = 0xffffffff, LENGTH = 0
}

SECTIONS {
	.start : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		bsp_section_start_begin = .;
		KEEP (*(.bsp_start_text))
		KEEP (*(.bsp_start_data))
		bsp_section_start_end = .;
	} > REGION_START AT > REGION_START
	bsp_section_start_size = bsp_section_start_end - bsp_section_start_begin;

	.text : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		bsp_section_text_begin = .;
		*(.text.unlikely .text.*_unlikely .text.unlikely.*)
		*(.text.exit .text.exit.*)
		*(.text.startup .text.startup.*)
		*(.text.hot .text.hot.*)
		*(.text .stub .text.* .gnu.linkonce.t.*)
		/* .gnu.warning sections are handled specially by elf32.em.	*/
		*(.gnu.warning)
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.init : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		KEEP (*(SORT_NONE(.init)))
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	.fini : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		KEEP (*(SORT_NONE(.fini)))
		bsp_section_text_end = .;
	} > REGION_TEXT AT > REGION_TEXT_LOAD
	bsp_section_text_size = bsp_section_text_end - bsp_section_text_begin;
	bsp_section_text_load_begin = LOADADDR (.text);
	bsp_section_text_load_end = bsp_section_text_load_begin + bsp_section_text_size;

	.robarrier : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		. = ALIGN (bsp_section_robarrier_align);
	} > REGION_RODATA AT > REGION_RODATA

	.rodata : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		bsp_section_rodata_begin = .;
		*(.rodata .rodata.* .gnu.linkonce.r.*)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.rodata1 : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.rodata1)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.sdata2 : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.sdata2 .sdata2.* .gnu.linkonce.s2.*)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.sbss2 : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.sbss2 .sbss2.* .gnu.linkonce.sb2.*)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.eh_frame_hdr : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.eh_frame_hdr) *(.eh_frame_entry .eh_frame_entry.*)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.eh_frame : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		KEEP (*(.eh_frame)) *(.eh_frame.*)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.gcc_except_table : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.gcc_except_table .gcc_except_table.*)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.gnu_extab : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.gnu_extab*)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.tdata : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		_TLS_Data_begin = .;
		*(.tdata .tdata.* .gnu.linkonce.td.*)
		_TLS_Data_end = .;
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.tbss : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		_TLS_BSS_begin = .;
		*(.tbss .tbss.* .gnu.linkonce.tb.*) *(.tcommon)
		_TLS_BSS_end = .;
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	_TLS_Data_size = _TLS_Data_end - _TLS_Data_begin;
	_TLS_Data_begin = _TLS_Data_size != 0 ? _TLS_Data_begin : _TLS_BSS_begin;
	_TLS_Data_end = _TLS_Data_size != 0 ? _TLS_Data_end : _TLS_BSS_begin;
	_TLS_BSS_size = _TLS_BSS_end - _TLS_BSS_begin;
	_TLS_Size = _TLS_BSS_end - _TLS_Data_begin;
	_TLS_Alignment = MAX (ALIGNOF (.tdata), ALIGNOF (.tbss));
	.preinit_array : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		PROVIDE_HIDDEN (__preinit_array_start = .);
		KEEP (*(.preinit_array))
		PROVIDE_HIDDEN (__preinit_array_end = .);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.init_array : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		PROVIDE_HIDDEN (__init_array_start = .);
		KEEP (*(SORT_BY_INIT_PRIORITY(.init_array.*) SORT_BY_INIT_PRIORITY(.ctors.*)))
		KEEP (*(.init_array EXCLUDE_FILE (*crtbegin.o *crtbegin?.o *crtend.o *crtend?.o ) .ctors))
		PROVIDE_HIDDEN (__init_array_end = .);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.fini_array : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		PROVIDE_HIDDEN (__fini_array_start = .);
		KEEP (*(SORT_BY_INIT_PRIORITY(.fini_array.*) SORT_BY_INIT_PRIORITY(.dtors.*)))
		KEEP (*(.fini_array EXCLUDE_FILE (*crtbegin.o *crtbegin?.o *crtend.o *crtend?.o ) .dtors))
		PROVIDE_HIDDEN (__fini_array_end = .);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.ctors : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		/* gcc uses crtbegin.o to find the start of
			 the constructors, so we make sure it is
			 first.	Because this is a wildcard, it
			 doesn't matter if the user does not
			 actually link against crtbegin.o; the
			 linker won't look for a file to match a
			 wildcard.	The wildcard also means that it
			 doesn't matter which directory crtbegin.o
			 is in.	*/
		KEEP (*crtbegin.o(.ctors))
		KEEP (*crtbegin?.o(.ctors))
		/* We don't want to include the .ctor section from
			 the crtend.o file until after the sorted ctors.
			 The .ctor section from the crtend file contains the
			 end of ctors marker and it must be last */
		KEEP (*(EXCLUDE_FILE (*crtend.o *crtend?.o ) .ctors))
		KEEP (*(SORT(.ctors.*)))
		KEEP (*(.ctors))
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.dtors : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		KEEP (*crtbegin.o(.dtors))
		KEEP (*crtbegin?.o(.dtors))
		KEEP (*(EXCLUDE_FILE (*crtend.o *crtend?.o ) .dtors))
		KEEP (*(SORT(.dtors.*)))
		KEEP (*(.dtors))
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.data.rel.ro : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.data.rel.ro.local* .gnu.linkonce.d.rel.ro.local.*)
		*(.data.rel.ro .data.rel.ro.* .gnu.linkonce.d.rel.ro.*)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.jcr : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		KEEP (*(.jcr))
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.interp : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.interp)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.note.gnu.build-id : { *(.note.gnu.build-id)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.hash : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.hash)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.gnu.hash : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.gnu.hash)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.dynsym : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.dynsym)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.dynstr : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.dynstr)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.gnu.version : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.gnu.version)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.gnu.version_d : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.gnu.version_d)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.gnu.version_r : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.gnu.version_r)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.rela.dyn : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.rela.init)
		*(.rela.text .rela.text.* .rela.gnu.linkonce.t.*)
		*(.rela.fini)
		*(.rela.rodata .rela.rodata.* .rela.gnu.linkonce.r.*)
		*(.rela.data .rela.data.* .rela.gnu.linkonce.d.*)
		*(.rela.tdata .rela.tdata.* .rela.gnu.linkonce.td.*)
		*(.rela.tbss .rela.tbss.* .rela.gnu.linkonce.tb.*)
		*(.rela.ctors)
		*(.rela.dtors)
		*(.rela.got)
		*(.rela.sdata .rela.sdata.* .rela.gnu.linkonce.s.*)
		*(.rela.sbss .rela.sbss.* .rela.gnu.linkonce.sb.*)
		*(.rela.sdata2 .rela.sdata2.* .rela.gnu.linkonce.s2.*)
		*(.rela.sbss2 .rela.sbss2.* .rela.gnu.linkonce.sb2.*)
		*(.rela.bss .rela.bss.* .rela.gnu.linkonce.b.*)
		PROVIDE_HIDDEN (__rela_iplt_start = .);
		*(.rela.iplt)
		PROVIDE_HIDDEN (__rela_iplt_end = .);
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.rela.plt : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
			*(.rela.plt)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.plt : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.plt)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.iplt : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.iplt)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.dynamic : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.dynamic)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.tm_clone_table : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.tm_clone_table)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.got : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.got.plt) *(.igot.plt) *(.got) *(.igot)
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	.rtemsroset : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		/* Special FreeBSD linker set sections */
		__start_set_sysctl_set = .;
		*(set_sysctl_*);
		__stop_set_sysctl_set = .;
		*(set_domain_*);
		*(set_pseudo_*);

		KEEP (*(SORT(.rtemsroset.*)))
		bsp_section_rodata_end = .;
	} > REGION_RODATA AT > REGION_RODATA_LOAD
	bsp_section_rodata_size = bsp_section_rodata_end - bsp_section_rodata_begin;
	bsp_section_rodata_load_begin = LOADADDR (.rodata);
	bsp_section_rodata_load_end = bsp_section_rodata_load_begin + bsp_section_rodata_size;

	.rwbarrier : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		. = ALIGN (bsp_section_rwbarrier_align);
	} > REGION_DATA AT > REGION_DATA

	.fast_text : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		bsp_section_fast_text_begin = .;
		*(.bsp_fast_text)
		bsp_section_fast_text_end = .;
	} > REGION_FAST_TEXT AT > REGION_FAST_TEXT_LOAD
	bsp_section_fast_text_size = bsp_section_fast_text_end - bsp_section_fast_text_begin;
	bsp_section_fast_text_load_begin = LOADADDR (.fast_text);
	bsp_section_fast_text_load_end = bsp_section_fast_text_load_begin + bsp_section_fast_text_size;

	.fast_data : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		bsp_section_fast_data_begin = .;
		*(.bsp_fast_data)
		bsp_section_fast_data_end = .;
	} > REGION_FAST_DATA AT > REGION_FAST_DATA_LOAD
	bsp_section_fast_data_size = bsp_section_fast_data_end - bsp_section_fast_data_begin;
	bsp_section_fast_data_load_begin = LOADADDR (.fast_data);
	bsp_section_fast_data_load_end = bsp_section_fast_data_load_begin + bsp_section_fast_data_size;

	.data : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		bsp_section_data_begin = .;
		*(.data .data.* .gnu.linkonce.d.*)
		SORT(CONSTRUCTORS)
	} > REGION_DATA AT > REGION_DATA_LOAD
	.data1 : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.data1)
	} > REGION_DATA AT > REGION_DATA_LOAD
	.rtemsrwset : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		KEEP (*(SORT(.rtemsrwset.*)))
	} > REGION_DATA AT > REGION_DATA_LOAD
	.htif ALIGN(4096) : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.htif)
	} > REGION_DATA AT > REGION_DATA_LOAD
	.sdata : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		__global_pointer$ = . + 0x800;
		*(.srodata.cst16) *(.srodata.cst8) *(.srodata.cst4) *(.srodata.cst2) *(.srodata .srodata.*)
		*(.sdata .sdata.* .gnu.linkonce.s.*)
		bsp_section_data_end = .;
	} > REGION_DATA AT > REGION_DATA_LOAD
	bsp_section_data_size = bsp_section_data_end - bsp_section_data_begin;
	bsp_section_data_load_begin = LOADADDR (.data);
	bsp_section_data_load_end = bsp_section_data_load_begin + bsp_section_data_size;

	.sbss : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		bsp_section_bss_begin = .;
		*(.dynsbss)
		*(.sbss .sbss.* .gnu.linkonce.sb.*)
		*(.scommon)
	} > REGION_DATA AT > REGION_DATA
	.bss : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		*(.dynbss)
		*(.bss .bss.* .gnu.linkonce.b.*)
		*(COMMON)
		bsp_section_bss_end = .;
	} > REGION_DATA AT > REGION_DATA
	bsp_section_bss_size = bsp_section_bss_end - bsp_section_bss_begin;

	.rtemsstack (NOLOAD) : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		bsp_section_rtemsstack_begin = .;
		*(SORT(.rtemsstack.*))
		bsp_section_rtemsstack_end = .;
	} > REGION_RTEMSSTACK AT > REGION_RTEMSSTACK
	bsp_section_rtemsstack_size = bsp_section_rtemsstack_end - bsp_section_rtemsstack_begin;

	.work (NOLOAD) : @RISCV_LINKER_ALIGN_DIRECTIVE@ {
		/*
		 * The work section will occupy the remaining REGION_WORK region and
		 * contains the RTEMS work space and heap.
		 */
		bsp_section_work_begin = .;
		. += ORIGIN (REGION_WORK) + LENGTH (REGION_WORK) - ABSOLUTE (.);
		bsp_section_work_end = .;
	} > REGION_WORK AT > REGION_WORK
	bsp_section_work_size = bsp_section_work_end - bsp_section_work_begin;

	/* FIXME */
	RamBase = ORIGIN (REGION_WORK);
	RamSize = LENGTH (REGION_WORK);
	RamEnd = RamBase + RamSize;
	WorkAreaBase = bsp_section_work_begin;
	HeapSize = 0;

	/* Stabs debugging sections.	*/
	.stab					0 : { *(.stab) }
	.stabstr			 0 : { *(.stabstr) }
	.stab.excl		 0 : { *(.stab.excl) }
	.stab.exclstr	0 : { *(.stab.exclstr) }
	.stab.index		0 : { *(.stab.index) }
	.stab.indexstr 0 : { *(.stab.indexstr) }
	.comment			 0 : { *(.comment) }
	/* DWARF debug sections.
		 Symbols in the DWARF debugging sections are relative to the beginning
		 of the section so we begin them at 0.	*/
	/* DWARF 1 */
	.debug					0 : { *(.debug) }
	.line					 0 : { *(.line) }
	/* GNU DWARF 1 extensions */
	.debug_srcinfo	0 : { *(.debug_srcinfo) }
	.debug_sfnames	0 : { *(.debug_sfnames) }
	/* DWARF 1.1 and DWARF 2 */
	.debug_aranges	0 : { *(.debug_aranges) }
	.debug_pubnames 0 : { *(.debug_pubnames) }
	/* DWARF 2 */
	.debug_info		 0 : { *(.debug_info .gnu.linkonce.wi.*) }
	.debug_abbrev	 0 : { *(.debug_abbrev) }
	.debug_line		 0 : { *(.debug_line .debug_line.* .debug_line_end ) }
	.debug_frame		0 : { *(.debug_frame) }
	.debug_str			0 : { *(.debug_str) }
	.debug_loc			0 : { *(.debug_loc) }
	.debug_macinfo	0 : { *(.debug_macinfo) }
	/* SGI/MIPS DWARF 2 extensions */
	.debug_weaknames 0 : { *(.debug_weaknames) }
	.debug_funcnames 0 : { *(.debug_funcnames) }
	.debug_typenames 0 : { *(.debug_typenames) }
	.debug_varnames	0 : { *(.debug_varnames) }
	/* DWARF 3 */
	.debug_pubtypes 0 : { *(.debug_pubtypes) }
	.debug_ranges	 0 : { *(.debug_ranges) }
	/* DWARF Extension.	*/
	.debug_macro		0 : { *(.debug_macro) }
	.debug_addr		 0 : { *(.debug_addr) }
	.gnu.attributes 0 : { KEEP (*(.gnu.attributes)) }
	/DISCARD/ : { *(.note.GNU-stack) *(.gnu_debuglink) *(.gnu.lto_*) }
	.shstrtab		 0 : { *(.shstrtab) }
	.strtab		 0 : { *(.strtab) }
	.symtab		 0 : { *(.symtab) }
	.symtab_shndx		 0 : { *(.symtab_shndx) }
	__cap_relocs		 0 : { *(__cap_relocs) }

	/*
	 * This is a RTEMS specific section to catch all unexpected input
	 * sections.  In case you get an error like
	 *   "section `.unexpected_sections' will not fit in region
	 *   `UNEXPECTED_SECTIONS'"
	 * you have to figure out the offending input section and add it to the
	 * appropriate output section definition above.
	 */
	.unexpected_sections : { *(*) } > UNEXPECTED_SECTIONS
}
