MEMORY {
	DSROM : ORIGIN = 0x00020000, LENGTH = 64k
	SYSROM : ORIGIN = 0x00100000, LENGTH = 4k
	RAM : ORIGIN = 0x40000000, LENGTH = 240k
	NOCACHE : ORIGIN = 0x4003c000, LENGTH = 16k
	DSRAM : ORIGIN = 0x20000000, LENGTH = 64k
	RAM_EXT : ORIGIN = 0x20010000, LENGTH = 444k
	SYSRAM : ORIGIN = 0x2007f000, LENGTH = 4k
	NVRAM : ORIGIN = 0x3ffa0000, LENGTH = 128k
}

REGION_ALIAS ("REGION_START", STARTROM);
REGION_ALIAS ("REGION_FAST_TEXT", RAM);
REGION_ALIAS ("REGION_FAST_TEXT_LOAD", ROM);
REGION_ALIAS ("REGION_TEXT", ROM);
REGION_ALIAS ("REGION_TEXT_LOAD", ROM);
REGION_ALIAS ("REGION_RODATA", ROM);
REGION_ALIAS ("REGION_RODATA_LOAD", ROM);
REGION_ALIAS ("REGION_FAST_DATA", RAM);
REGION_ALIAS ("REGION_FAST_DATA_LOAD", ROM);
REGION_ALIAS ("REGION_DATA", RAM);
REGION_ALIAS ("REGION_DATA_LOAD", ROM);
REGION_ALIAS ("REGION_BSS", RAM);
REGION_ALIAS ("REGION_RTEMSSTACK", RAM_EXT);
REGION_ALIAS ("REGION_WORK", RAM);
REGION_ALIAS ("REGION_STACK", RAM);
REGION_ALIAS ("REGION_NOCACHE", NOCACHE);
REGION_ALIAS ("REGION_NOCACHE_LOAD", ROM);
REGION_ALIAS ("REGION_NVRAM", NVRAM);

SECTIONS {
	.dsram (NOLOAD) : {
		bsp_section_dsram_begin = .;
		*(SORT(.bsp_dsram*))
		bsp_section_dsram_end = .;
	} > DSRAM AT > DSROM
	bsp_section_dsram_size = bsp_section_dsram_end - bsp_section_dsram_begin;
	bsp_section_dsram_load_begin = LOADADDR (.dsram);
	bsp_section_dsram_load_end = bsp_section_dsram_load_begin + bsp_section_dsram_size;

	.sysram : {
		bsp_section_sysram_begin = .;
		*(SORT(.bsp_sysram*))
		bsp_section_sysram_end = .;
	} > SYSRAM AT > SYSROM
	bsp_section_sysram_size = bsp_section_sysram_end - bsp_section_sysram_begin;
	bsp_section_sysram_load_begin = LOADADDR (.sysram);
	bsp_section_sysram_load_end = bsp_section_sysram_load_begin + bsp_section_sysram_size;
}

INCLUDE linkcmds.mpc55xx
