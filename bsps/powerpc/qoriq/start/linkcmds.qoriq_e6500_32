/**
 * @file
 *
 * Memory map for e6500 core based QorIQ chips, e.g. T2080, T4240.
 */

EXTERN (__vectors)

MEMORY {
	LOW     : ORIGIN = 0x00004000, LENGTH = 16M - 16k
	NOCACHE : ORIGIN = 0x01000000, LENGTH = 48M
	HIGH    : ORIGIN = 0x04000000, LENGTH = 512M - 64M
	EMPTY   : ORIGIN = 0x00000000, LENGTH = 0
}

REGION_ALIAS ("REGION_START", LOW);
REGION_ALIAS ("REGION_FAST_TEXT", LOW);
REGION_ALIAS ("REGION_FAST_TEXT_LOAD", LOW);
REGION_ALIAS ("REGION_TEXT", LOW);
REGION_ALIAS ("REGION_TEXT_LOAD", LOW);
REGION_ALIAS ("REGION_RODATA", HIGH);
REGION_ALIAS ("REGION_RODATA_LOAD", LOW);
REGION_ALIAS ("REGION_FAST_DATA", HIGH);
REGION_ALIAS ("REGION_FAST_DATA_LOAD", LOW);
REGION_ALIAS ("REGION_DATA", HIGH);
REGION_ALIAS ("REGION_DATA_LOAD", LOW);
REGION_ALIAS ("REGION_BSS", HIGH);
REGION_ALIAS ("REGION_RTEMSSTACK", HIGH);
REGION_ALIAS ("REGION_WORK", HIGH);
REGION_ALIAS ("REGION_STACK", HIGH);
REGION_ALIAS ("REGION_NOCACHE", NOCACHE);
REGION_ALIAS ("REGION_NOCACHE_LOAD", LOW);
REGION_ALIAS ("REGION_NVRAM", EMPTY);

bsp_section_robarrier_align = 0x1000000;
bsp_section_rwbarrier_align = 0x1000000;
qoriq = 0xffe000000;
qoriq_bman_portal = 0xff4000000;
qoriq_qman_portal = 0xff6000000;

/* Allocate the maximum size to the small data area */
bsp_section_small_data_area_size = 65536;

INCLUDE linkcmds.base
